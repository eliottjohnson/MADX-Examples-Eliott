!-----------------------------------------------------------------------!
! general ejection elements                                             !
!-----------------------------------------------------------------------!

MTV001  : MONITOR;  ! luminescent screen on septum
pointR  : MARKER;   ! exit ring stray field, upstream of 1st quad
pointS  : MARKER;   ! exit septum
KFA7179 : HKICKER,KICK=-KFA*166.78E-6/BRHO;





!-----------------------------------------------------------------------!
!                             injection 42                              !
!-----------------------------------------------------------------------!

kicker45 : HKICKER, L=0.8, KICK  = KIC45 * 0.001;
septum42 : RBEND, L=0.8, ANGLE = -.050;
D41STRAY : SBEND, L=LD+DLD, ANGLE=0.008  , K1=-0.026, K2=+0.222;
F41STRAY : DRIFT,  L=LF+DLF;
DXX42    : = .105664; ! beam position at exit septum 42
DPX42    : = .013259; ! - beam angle  at exit septum 42

!FT42     : LINE=(pointS,septum42,dr02,c,D41STRAY,F41STRAY,pointR);
FT42     : LINE=(pointS,septum42,dr02,c,D41STRAY,F41STRAY,c,pointR);
FE42     : LINE=(-SEC0,-SEC9,-SEC8,-SEC7,-SEC6,-U,-PS50,-T,-PS49,-U, &
                -PS48, -T, -PS47,-U, -PS46,-T,QNF,dr02,kicker45, &
                PR.BPM45,-U, -PS44,-T, -PS43,-U);
FI42     : LINE=(-FE42);

!


!-----------------------------------------------------------------------!
!                           fast ejection 16                            !
!-----------------------------------------------------------------------!



SEPTUM16: RBEND,L=2.4,ANGLE=-0.027;
D16STRAY: RBEND,L=2.20480,ANGLE=0.015,K1=-.047,K2=0.33;
F16SHIM : RBEND,L=2.19624,ANGLE=0.021,K1=0.0;
FT16    : LINE=(MTV001,SEPTUM16,dr03,D16STRAY,F16SHIM,dr06,pointR);
FE16T0  : LINE=(SEC1,SEC2,SEC3,SEC4,SEC5,SEC6,SEC7,          &
                KFA7179,SEC8,SEC9,SEC0);
!FE16T1  : LINE= (SEC1,PS11,T,PS12,U,PS13,T,PS14,U,PS15,T);
FE16T1  : LINE= (SEC1,PS11,S,PS12,R,PS13,S,PS14,U,PS15,T);
FE16    : LINE= (FE16T0, FE16T1);




!
!-----------------------------------------------------------------------!
!                         slow ejection 61,                             !
!-----------------------------------------------------------------------!

! Calculate B.rho using the dipole component K0 of the bending magnets
! to normalize the gradients, if these are entered in Tesla/meter

BDL     :=25.0*(LF+DLF+LD+DLD)*(0.909*(KI0D+KE0D)+1.091*(KI0F+KE0F));
BRHOP   :=BDL / TWOPI;  ! Used only by extraction 61 in element QFO001.
                        ! WARNING: Value of BRHOP ~30% bigger than BRHO


QFO001      : QUADRUPOLE, L=.800, K1=0.0638*610.0/(BRHOP);
PIPE63      : DRIFT, L=4.40;
MTV002      : MARKER;

!measured PS magnet F,  Ip=4732,  Ipf=78.0,  Ibp8=0.0
!  0.19500 X offset
!  0.16000 X min
!  0.23000 X max
!  0.02000 coil dim,   0.03000 coil sep
!F62STRAY: SBEND, L=2.2, ANGLE=0.015, K1=-0.122, K2=+1.65;
F62STRAY: SBEND, L=2.2, ANGLE=0.0202, K1=-0.122, K2=+1.65; ! (27/5/94)


!measure  PS magnet D,  Ip=4732,  Ipf=75.0,  Ibp8=0.0
!  0.13500 X offset
!  0.11000 X min
!  0.16000 X max
!  0.02000 coil dim,   0.03000 coil sep
D62STRAY: SBEND, L=2.2, ANGLE=0.0148, K1=-0.0465, K2=+0.294; ! (27/5/94)
!D62STRAY: SBEND, L=2.2, ANGLE=0.015, K1=-0.0468, K2=+0.286

SEPTUM23 : RCOLLIMATOR,    XSIZE =   .072;
SEPTUM57 : HKICKER, L = 0.0, KICK  = +0.0039;
SEPTUM61 : HKICKER, L = 2.4, KICK  = +0.0012;

FT62: LINE=(dr03, D62STRAY, F62STRAY, dr04, pointR);
!          (QFO001, dr04, PIPE63, dr03, MTV002)

FT61: LINE=(dr08, T, dr10);

ST5461: LINE=(PR.MBP54,R,PS55,T,PS56,                                   &
              U,PS57,T,PS58,U,PS59,T,PS60,U,PS61); !M.M 25/1/95
ST5561: LINE=(stripper,T,PS56,                                       &
              U,PS57,T,PS58,U,PS59,T,PS60,U,PS61); !M.M 25/1/95

SE23: LINE=(PS23,T,PS24,U,PS25,T,PS26,U,PS27,T,PS28,U,PS29,T,PS30,U, &
            SEC4,SEC5,SEC6,SEC7,SEC8,SEC9,SEC0,SEC1,SEC2,            &
            PS21,T,PS22,U,SEPTUM23);

SE61: LINE=(SEPTUM23,                                                &
            PS23,T,PS24,U,PS25,T,PS26,U,PS27,T,PS28,U,PS29,T,PS30,U, &
            SEC4,SEC5,                                               &
            PS51,T,PS52,U,PS53,T,PS54,U,PS55,T,PS56,U,SEPTUM57,      &
            PS57,T,PS58,U,PS59,T,PS60,U,                             &
            SEPTUM61,T,MTV001,dr10);

!

!-----------------------------------------------------------------------!
! CT 14 GeV/c                                                           !
!-----------------------------------------------------------------------!

SEPTUM31: HKICKER,KICK=+0.0008;
BFA1:=0.0;
BFA2:=0.0;
BFAS1 :  HKICKER,  KICK := BFA1;
BFAS2 :  HKICKER,  KICK := BFA2;
!BFA's in straight-sections 9-21
SEC11 :LINE=(PS01,T,PS02,U,PS03,T,PS04,R,PS05,T,                 &
             PS06,R,PS07,S,PS08,R,BFAS2,PS09,T,PS10,R);
!BFA's in straight-sections 10-21
!SEC11 :LINE=(PS01,T,PS02,U,PS03,T,PS04,R,PS05,T,                 &
!             PS06,R,PS07,S,PS08,R,PS09,T,BFAS2,PS10,R)
PSCT0   : LINE=(PS16,U,PS17,T,PS18,U,PS19,T,PS20,U,              &
                SEC3,SEC4,SEC5,SEC6,SEC7,SEC8,SEC9,SEC0,SEC1,    &
                PS11,T,PS12,U,PS13,T,PS14,U,PS15,T);

PSCTT   : LINE=(PS16,U,PS17,T,PS18,U,PS19,T,PS20,U,BFAS1,        &
                SEC3,SEC4,SEC5,SEC6,SEC7,SEC8,SEC9,SEC0,SEC11,   &
                PS11,T,PS12,U,PS13,T,PS14,U,PS15,T);

PSCTL   : LINE=(PS16,U,PS17,T,PS18,U,PS19,T,PS20,U,BFAS1,        &
                SEC3,SEPTUM31,SEC4,SEC5,SEC6,SEC7,SEC8,SEC9,SEC0,SEC11, &
                PS11,T,PS12,U,PS13,T,PS14,U,PS15,SEPTUM16);

PSCT1 : LINE = (PSCT0,          PSCTL);
PSCT2 : LINE = (PSCT0, 1*PSCTT, PSCTL);
PSCT3 : LINE = (PSCT0, 2*PSCTT, PSCTL);
PSCT4 : LINE = (PSCT0, 3*PSCTT, PSCTL);
PSCT5 : LINE = (PSCT0, 4*PSCTT, PSCTL);

