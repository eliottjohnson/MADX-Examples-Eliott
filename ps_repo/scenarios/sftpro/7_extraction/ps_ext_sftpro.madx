/**********************************************************************************
*
* MAD-X input script for the MTE extraction configuration on the SFTPRO cycle.
* 21/10/2019 - Alexander Huschauer
************************************************************************************/
 
/******************************************************************
 * Energy and particle type definition
 ******************************************************************/

call, file="ps_ext_sftpro.beam";
BRHO      := BEAM->PC * 3.3356;

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file="../../../ps_mu.seq";
call, file="../../../ps_ss.seq";
call, file="./ps_ext_sftpro.str";
call, file="../../../_scripts/macros.madx";

/******************************************************************
 * Define fourfold PS sequence for Twiss in the islands
 ******************************************************************/

use, sequence = PS;
twiss;
PSL = table(summ, length);

PS4: SEQUENCE, L = 4*PSL, refer = entry;
PS, at = 0.0;
PS, at = PSL;
PS, at = 2*PSL;
PS, at = 3*PSL;
ENDSEQUENCE;

/**********************************************************************************
 * PTC Twiss for the core
***********************************************************************************/

use, sequence=PS;
exec, ptc_twiss_macro(2,0,0);
exec, write_ptc_twiss("ps_ext_sftpro.tfs");

stop;

/**********************************************************************************
 * PTC Twiss for the islands
***********************************************************************************/

x = 0.051;
px = -0.0016;

use, sequence=PS4;
exec, ptc_twiss_MTE_macro(2,x,px,0);
exec, write_ptc_twiss("ps_ext_sftpro_islands.tfs");

/**********************************************************************************
 *                                  Tune matching
 *         Matching has to be performed with extraction bumpers OFF
***********************************************************************************/

kPEBSW12 =  0.0;
kPEBSW14 =  0.0;
kPEBSW20 =  0.0;
kPEBSW22 =  0.0;

Qx := 0.268507;
! vertical tune according to 2015 chromaticity measurement
Qy := 0.29826;

use, sequence=PS;
match, use_macro;
        vary, name = kf;
        vary, name = kd;
        use_macro, name = ptc_twiss_macro(2,0,0);
        constraint, expr = table(ptc_twiss_summary,Q1)  = Qx;
        constraint, expr = table(ptc_twiss_summary,Q2)  = Qy;
jacobian,calls=50000,bisec=3, tolerance = 1E-15;
ENDMATCH;

! Settings of extraction elements
CC205 := 330.E-6 / BRHO;

kPEBSW12 =  413.36 * CC205;
kPEBSW14 = -443.47 * CC205;
kPEBSW20 =  413.36 * CC205;
kPEBSW22 = -437.71 * CC205;

exec, write_str_file("ps_ext_sftpro.str");
