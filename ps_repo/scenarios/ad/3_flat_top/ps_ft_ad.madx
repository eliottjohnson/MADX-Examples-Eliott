/**********************************************************************************
*
* MAD-X input script for the flat top optics of the AD cycle.
* 17/10/2019 - Alexander Huschauer
************************************************************************************/
 
/******************************************************************
 * Energy and particle type definition
 ******************************************************************/

call, file="ps_ft_ad.beam";
BRHO      := BEAM->PC * 3.3356;

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file="../../../ps_mu.seq";
call, file="../../../ps_ss.seq";
call, file="./ps_ft_ad.str";
call, file="../../../_scripts/macros.madx";

/**********************************************************************************
 * PTC Twiss
***********************************************************************************/

use, sequence=PS;
exec, ptc_twiss_macro(2,0,0);
exec, write_ptc_twiss("ps_ft_ad.tfs");

stop;

/**********************************************************************************
 *                            Matching using the PFW
 *
 * Values based on non-linear chromaticity measurement above   
 * transition recorded on 29.10.2018
***********************************************************************************/

! Qx = 0.2415 + 0.7141*x + -150.27302*x^2
Qx   := 0.2415;
Qxp  := 0.7141;
Qxp2 := -150.27302;

! Qy = 0.2629 + 1.1024*x + -74.78295*x^2
Qy   := 0.2629;
Qyp  := 1.1024;
Qyp2 := -74.78295;

! During the measurements a delta of dI_W8L = 35A has been applied to separate the tunes.
! dQ * (-1) has to be added to Qi to correct for this effect.
dQx = -0.021606068;
dQy =  0.020953456;

use, sequence=PS;
match, use_macro;
        vary, name = k1prpfwf;
        vary, name = k1prpfwd;
        vary, name = k2prpfwf;
        vary, name = k2prpfwd;
        use_macro, name = ptc_twiss_macro(2,0,0);
        constraint, expr = table(ptc_twiss_summary,Q1)  = Qx - dQx;
        constraint, expr = table(ptc_twiss_summary,Q2)  = Qy - dQy;
        constraint, expr = table(ptc_twiss_summary,DQ1) = Qxp;
        constraint, expr = table(ptc_twiss_summary,DQ2) = Qyp;
jacobian,calls=50000,bisec=3;
ENDMATCH;

use, sequence=PS;
match, use_macro;
        vary, name = k3prpfwf;
        vary, name = k3prpfwd;
        use_macro, name = ptc_twiss_macro(3,0,0);
        constraint, expr = table(nonlin,value,5)  = Qxp2;
        constraint, expr = table(nonlin,value,10) = Qyp2;
jacobian,calls=50000,bisec=3;
ENDMATCH;

exec, write_str_file("ps_ft_ad.str");

/**********************************************************************************
 * Non-linear chromaticity 
***********************************************************************************/

use, sequence=PS;

create, table=mytable, column = dp, qx, qy;

qx := table(ptc_twiss_summary, q1);
qy := table(ptc_twiss_summary, q2);

dp = -0.01;
dp_max = 0.011;

while (dp <= dp_max){
        exec, ptc_twiss_macro(2,dp,0);
        fill, table = mytable; write, table=mytable, file = './ps_ft_ad_chromaticity_summary.out';
        dp = dp + 0.001;
}
