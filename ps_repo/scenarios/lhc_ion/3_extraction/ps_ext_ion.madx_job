system, "mkdir output";

/**********************************************************************************
*
* MAD-X input script for the flat top optics of the LHC ion cycle.
* 30/03/2020 - Alexander Huschauer
************************************************************************************/
 
/******************************************************************
 * Energy and particle type definition
 ******************************************************************/

call, file="ps_ext_ion.beam";
BRHO      := BEAM->PC * 3.3356 * 208./54.;

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file="ps_mu.seq";
call, file="ps_ss.seq";
call, file="ps_ext_ion.str";
call, file="macros.madx";

exec, unassign_QKE16;
exec, unassign_BSW16;

/**********************************************************************************
 *                        Matching using the PFW
 *
 * Values based on non-linear chromaticity measurement at flat top 
 * recorded on 07.12.2018
***********************************************************************************/

! Qx = 0.23802 + 0.78214*x + -317.07576*x^2
Qx   := 0.23802;
Qxp  := 0.78214;
Qxp2 := -317.07576;

! Qy = 0.27424 + -0.07398*x + -52.17227*x^2
Qy   := 0.27424;
Qyp  := -0.07398;
Qyp2 := -52.17227;

use, sequence=PS;
match, use_macro;
        vary, name = k1prpfwf;
        vary, name = k1prpfwd;
        vary, name = k2prpfwf;
        vary, name = k2prpfwd;
        use_macro, name = ptc_twiss_macro(2,0,0);
        constraint, expr = table(ptc_twiss_summary,Q1)  = Qx;
        constraint, expr = table(ptc_twiss_summary,Q2)  = Qy;
        constraint, expr = table(ptc_twiss_summary,DQ1) = Qxp;
        constraint, expr = table(ptc_twiss_summary,DQ2) = Qyp;
jacobian,calls=50000,bisec=3;
ENDMATCH;

use, sequence=PS;
match, use_macro;
        vary, name = k3prpfwf;
        vary, name = k3prpfwd;
        use_macro, name = ptc_twiss_macro(3,0,0);
        constraint, expr = table(nonlin,value,5)  = Qxp2;
        constraint, expr = table(nonlin,value,10) = Qyp2;
jacobian,calls=50000,bisec=3;
ENDMATCH;

/**********************************************************************************
* Extraction bump matching
* The bump amplitude is based on 2018 LSA settings, when operationally all four bumpers
* were used 
* This matching uses all bumpers to close the bump in addition.
***********************************************************************************/

use, sequence=PS;

! 2018 settings as initial conditions
kpeqke16 = qke16;
kpebsw12           =    0.001037683841127277;
kpebsw14           =    0.0026778848721179297;
kpebsw20           =    0.001189068908131695;
kpebsw22           =    0.002782726941513182;

x0 = 0.0336;
px0 = 0.002;

exec, match_bsw16_bump(1);

/**********************************************************************************
* Extraction bump knobs
***********************************************************************************/

exec, bsw16_knob_factors(1);

/**********************************************************************************
* High energy orbit corrector knobs
***********************************************************************************/

exec, dhzoc_knob_factors;

exec, write_str_file("./output/ps_ext_ion.str");

/**********************************************************************************
 * PTC Twiss
***********************************************************************************/

! obtain smooth optics functions by slicing the elements
use, sequence=PS;
exec, ptc_twiss_macro(2,0,1);
exec, write_ptc_twiss("./output/ps_ext_ion.tfs");
