system, "mkdir output";

/**********************************************************************************
*
* MAD-X input script for the extraction optics of the TOF cycle.
* 16/03/2021 - Alexander Huschauer
************************************************************************************/
 
/******************************************************************
 * Energy and particle type definition
 ******************************************************************/

call, file="ps_ext_tof.beam";
BRHO      := BEAM->PC * 3.3356;

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file="ps_mu.seq";
call, file="ps_ss.seq";
call, file="ps_ext_tof.str";
call, file="macros.madx";

exec, unassign_QKE16;
exec, unassign_BSW16;

/**********************************************************************************
 *                        Matching using the PFW
 *
 * Values based on non-linear chromaticity measurement along the cycle 
 * recorded on 01.11.2018
***********************************************************************************/

! Qx = 0.13094 + 1.22881*x + 19.68008*x^2
Qx   := 0.13094;
Qxp  := 1.22881;
Qxp2 := 19.68008;

! Qy = 0.40405 + 0.86712*x + -24.30586*x^2
Qy   := 0.40405;
Qyp  := 0.86712;
Qyp2 := -24.30586;

use, sequence=PS;
match, use_macro;
        vary, name = k1prpfwf;
        vary, name = k1prpfwd;
        vary, name = k2prpfwf;
        vary, name = k2prpfwd;
        use_macro, name = ptc_twiss_macro(2,0,0);
        constraint, expr = table(ptc_twiss_summary,Q1)  = Qx;
        constraint, expr = table(ptc_twiss_summary,Q2)  = Qy;
        constraint, expr = table(ptc_twiss_summary,DQ1) = Qxp;
        constraint, expr = table(ptc_twiss_summary,DQ2) = Qyp;
jacobian,calls=50000,bisec=3;
ENDMATCH;

use, sequence=PS;
match, use_macro;
        vary, name = k3prpfwf;
        vary, name = k3prpfwd;
        use_macro, name = ptc_twiss_macro(3,0,0);
        constraint, expr = table(nonlin,value,5)  = Qxp2;
        constraint, expr = table(nonlin,value,10) = Qyp2;
jacobian,calls=50000,bisec=3;
ENDMATCH;

/**********************************************************************************
* Extraction bump matching
* The bump amplitude is based on 2018 LSA settings, using only BSW14 and BSW22.
***********************************************************************************/

use, sequence=PS;

! 2018 settings as initial conditions
kpeqke16 = qke16;
kpebsw14           =    0.0033954172838194315;
kpebsw22           =    0.0034976645502748926;

x0 = 0.027;
xmax = 2E-3;

exec, match_bsw16_bump();  

/**********************************************************************************
* Extraction bump knobs
***********************************************************************************/

exec, bsw16_knob_factors();

/**********************************************************************************
* High energy orbit corrector knobs
***********************************************************************************/

exec, dhzoc_knob_factors;
exec, write_str_file("./output/ps_ext_tof.str");

/**********************************************************************************
 * PTC Twiss
***********************************************************************************/

! obtain smooth optics functions by slicing the elements
use, sequence=PS;
exec, ptc_twiss_macro(2,0,1);
exec, write_ptc_twiss("./output/ps_ext_tof.tfs");
